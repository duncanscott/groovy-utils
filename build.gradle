plugins {
    id 'idea'
    id 'com.jfrog.artifactory' version "latest.release"
}

repositories {
    mavenCentral()
}

def javaProjects() {
    subprojects.findAll { new File(it.projectDir, 'src').directory }
}

configure(javaProjects()) {
    apply plugin: 'groovy'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation group: 'org.apache.groovy', name: 'groovy', version: groovyVersion
        implementation group: 'org.slf4j', name: 'slf4j-api', version: sl4jVersion
        testRuntimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
        testRuntimeOnly group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion
    }

    testing {
        suites {
            test {
                useSpock(spockVersion)
            }
        }
    }

    tasks.register('sourcesJar', Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allGroovy
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact(file("$rootDir/gradle.properties"))
            }
        }
    }

    artifactory {
        publish {
            contextUrl = System.getenv('ARTIFACTORY_CONTEXT_URL')
            repository {
                repoKey = version.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
                username = System.getenv('ARTIFACTORY_USER')
                password = System.getenv('ARTIFACTORY_PASSWORD')
            }
            defaults {
                publications('mavenJava')
                publishPom = false
            }
        }
    }
}

artifactoryPublish.skip = true